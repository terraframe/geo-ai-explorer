
======
1. Wipe any existing data
======

PREFIX cwbi: <http://dime.usace.mil/ontologies/cwbi-concept#>

DELETE {
  GRAPH ?g1 {
    ?rem_proj cwbi:ProjectCost ?cost .
  }
}
WHERE {
  GRAPH ?g1 {
    ?rem_proj a cwbi:Remis_Project ;
              cwbi:ProjectCost ?cost .
  }
}

=

PREFIX cwbi: <http://dime.usace.mil/ontologies/cwbi-concept#>

DELETE {
  GRAPH ?g1 {
    ?program cwbi:ProgramBudget ?budget .
  }
}
WHERE {
  GRAPH ?g1 {
    ?program a cwbi:Program ;
              cwbi:ProgramBudget ?budget .
  }
}

=

PREFIX apex: http://dime.usace.mil/data/dataset#
PREFIX cwbi: http://dime.usace.mil/ontologies/cwbi-concept#
PREFIX pm: http://data.sec.usace.army.mil/ontologies/pm#
PREFIX pmcommon: http://data.sec.usace.army.mil/common/ont/pm#
PREFIX sdsfie: http://dime.usace.org/taxonomy/sdsfie/
PREFIX lpgs: https://localhost:4200/lpg/rdfs#
PREFIX lpg: https://localhost:4200/lpg#
PREFIX lpgv: https://localhost:4200/lpg/graph_801104/0#
PREFIX lpgvs: https://localhost:4200/lpg/graph_801104/0/rdfs#
PREFIX skos: http://www.w3.org/2004/02/skos/core#
PREFIX rdfs: http://www.w3.org/2000/01/rdf-schema#
PREFIX dct: http://purl.org/dc/terms/
PREFIX geo: http://www.opengis.net/ont/geosparql#
PREFIX xsd: http://www.w3.org/2001/XMLSchema#

DELETE {
	GRAPH https://localhost:4200/lpg/graph_801104/0#
	{
		?proj cwbi:ProjectCost ?cost .
	}
}
WHERE {
	GRAPH https://localhost:4200/lpg/graph_801104/0#
	{
		?proj a lpgvs:Project ;
		cwbi:ProjectCost ?cost .
	}
}


======
2. Generate new project costing data
======

PREFIX cwbi: <http://dime.usace.mil/ontologies/cwbi-concept#>
PREFIX lpgs: <https://localhost:4200/lpg/rdfs#>
PREFIX lpg: <https://localhost:4200/lpg#>
PREFIX lpgv: <https://localhost:4200/lpg/graph_801104/0#>
PREFIX lpgvs: <https://localhost:4200/lpg/graph_801104/0/rdfs#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

INSERT {
  GRAPH ?g1 {
    ?rem_proj cwbi:ProjectCost ?cost .
  }
}
WHERE {
  GRAPH ?g1 {
    ?rem_proj a cwbi:Remis_Project .
  }

  # random integer in [50,000, 150,000), mean â‰ˆ 100,000
  BIND(xsd:integer(50000 + FLOOR(RAND() * (150000 - 50000))) AS ?cost)
}

==
Add it to lpgvs:Project too so we can find it when we do attributes
==

PREFIX cwbi: <http://dime.usace.mil/ontologies/cwbi-concept#>
PREFIX lpgs: <https://localhost:4200/lpg/rdfs#>
PREFIX lpg: <https://localhost:4200/lpg#>
PREFIX lpgv: <https://localhost:4200/lpg/graph_801104/0#>
PREFIX lpgvs: <https://localhost:4200/lpg/graph_801104/0/rdfs#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

INSERT {
	GRAPH <https://localhost:4200/lpg/graph_801104/0#> {
		?proj cwbi:ProjectCost ?cost .
	}
}
WHERE {
	GRAPH <http://dime.usace.mil/data/dataset#REMIS_PROJECTS> {
		?rem_proj a cwbi:Remis_Project ;
			skos:altLabel ?projCode ;
			cwbi:ProjectCost ?cost .
	}
	
	GRAPH <https://localhost:4200/lpg/graph_801104/0#> {
		?proj a lpgvs:Project ;
			lpgs:GeoObject-code ?projCode .
	}
}


=====
3. Generate new program budget data
=====

PREFIX cwbi: <http://dime.usace.mil/ontologies/cwbi-concept#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>

INSERT {
  GRAPH ?g1 {
    ?program cwbi:ProgramBudget ?budget .
  }
}
WHERE {
  {
    SELECT ?g1 ?program (SUM(xsd:decimal(?cost)) AS ?totalCost)
    WHERE {
      GRAPH ?g1 {
        ?rem_proj a cwbi:Remis_Project ;
                  cwbi:Program ?program ;
                  cwbi:ProjectCost ?cost .
      }
    }
    GROUP BY ?g1 ?program
  }

  # percent in [-0.10 .. +0.10]
  BIND( ((RAND()*2.0) - 1.0) * 0.10 AS ?pct )

  # decimal math throughout; round to integer (or cents, see notes)
  BIND( xsd:decimal(?totalCost) * (1.0 + ?pct) AS ?rawBudget )
  BIND( xsd:integer(ROUND(?rawBudget)) AS ?budget )
}

=====
4. Query and verify
=====

PREFIX cwbi: <http://dime.usace.mil/ontologies/cwbi-concept#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?program
  (SAMPLE(?progLabel) AS ?programLabel)
  (SAMPLE(?bdg) AS ?budget)
  (COUNT(DISTINCT ?rem_proj) AS ?projectCount)
  (SUM(xsd:decimal(?cost))   AS ?totalCost)
  (AVG(xsd:decimal(?cost))   AS ?avgCost)
  (MIN(xsd:decimal(?cost))   AS ?minCost)
  (MAX(xsd:decimal(?cost))   AS ?maxCost)
  ( xsd:decimal(COALESCE(SAMPLE(?bdg), 0)) - SUM(xsd:decimal(?cost)) AS ?remainingBudget )
WHERE {
  GRAPH ?g1 {
    ?rem_proj a cwbi:Remis_Project ;
              cwbi:Program ?program ;
              cwbi:ProjectCost ?cost .
    OPTIONAL {
      ?program skos:prefLabel|skos:altLabel ?progLabel ;
               cwbi:ProgramBudget ?bdg
    }
  }
}
GROUP BY ?program
ORDER BY DESC(?totalCost)

==
Verify that the lpgv:Project data matches the cwbi data
==
PREFIX lpgs: <https://localhost:4200/lpg/rdfs#>
PREFIX lpgv: <https://localhost:4200/lpg/graph_801104/0#>
PREFIX lpgvs: <https://localhost:4200/lpg/graph_801104/0/rdfs#>
PREFIX cwbi: <http://dime.usace.mil/ontologies/cwbi-concept#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>

SELECT ?proj ?projCode ?label ?costDecimal
FROM <https://localhost:4200/lpg/graph_801104/0#>
WHERE {
  ?proj a lpgvs:Project ;
        lpgs:GeoObject-code ?projCode ;
        rdfs:label ?label ;
        cwbi:ProjectCost ?cost .
  BIND(xsd:decimal(?cost) AS ?costDecimal)
}
ORDER BY ?projCode
LIMIT 200

